plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'com.vanniktech.maven.publish'
}

group='com.github.thirstycoda'
version='5.0.0'

android {

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    defaultConfig {
        compileSdk project.androidCompileSdkVersion
        minSdkVersion project.androidMinSdkVersion
        targetSdkVersion project.androidTargetSdkVersion
        vectorDrawables.useSupportLibrary = true
    }

    kotlinOptions {
        jvmTarget = '17'
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            testCoverageEnabled true
        }
    }

    testOptions {
        //fixes https://github.com/robolectric/robolectric/issues/3169#issuecomment-312046322
        unitTests.includeAndroidResources = true
    }

    lintOptions {
        warningsAsErrors true
        disable 'RequiredSize', 'ClickableViewAccessibility'
    }

    namespace 'com.stepstone.stepper'
}

dependencies {
    api "androidx.appcompat:appcompat:1.0.0"

    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.mockito:mockito-core:$mockitoVersion"
    testImplementation "com.squareup.assertj:assertj-android:$assertjVersion"
    testImplementation("org.robolectric:robolectric:$robolectricVersion") {
        exclude group: 'commons-logging', module: 'commons-logging'
        exclude group: 'org.apache.httpcomponents', module: 'httpclient'
    }
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlinVersion"
    testImplementation "com.nhaarman:mockito-kotlin:$mockitoKotlinVersion"
}

// build a jar with source files
tasks.register('sourcesJar', Jar) {
    from android.sourceSets.main.java.srcDirs
    archiveClassifier = 'sources'
}

artifacts {
    archives sourcesJar
}

afterEvaluate {
    publishing {
        publications {
            myMaven(MavenPublication) {
                groupId = 'com.stepstone'
                artifactId = 'material-stepper'
                version = '5.0.0'

                // This must be set after evaluation because it's generated by another task
                def aarPath = "${buildDir}/outputs/aar/${project.name}-release.aar"

                artifact(aarPath) {
                    builtBy tasks.named("bundleReleaseAar")
                }
            }
        }
    }
}
